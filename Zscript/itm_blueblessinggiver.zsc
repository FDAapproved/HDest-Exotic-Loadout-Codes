const RFID_BLUEBLESSING="bls";


// Blue Blessing.
class HDBlueBlessingGiver:HDWeapon
{
	int amount;
	default
	{
		-HDWeapon.fitsinbackpack;
		HDWeapon.refid RFID_BLUEBLESSING;
		tag "Healing Energy";
		inventory.icon "PLSEB0";
		hdweapon.loadoutcodes "
			\cuamount - 0-???, amount to be blessed.
		";

	}

	override double weaponbulk()
	{
		return 300;
	}
	
	// Never shows in pickup manager, 
	// gives all directly to the player.
	override void postbeginplay()
	{	
	
		if(owner)
		{
			owner.A_StartSound("misc/p_pkup",CHAN_BODY);
			double oradius=owner.radius;
			double oheight=owner.height;
			vector3 ovel=owner.vel;
	
			if(amount <= -1 || amount == 0)
			{
				amount = random(48, HDBLU_BALL);
			}
	
			int remainder = amount;

			
			
			for(int i=0;i<20;i++)
			{
				vector3 rpos=owner.pos+(
					frandom(-oradius,oradius),
					frandom(-oradius,oradius),
					frandom(0,oheight)
				);
				actor spk=actor.spawn("BluesSpark",rpos,ALLOW_REPLACE);
				spk.vel=(frandom(-1,1),frandom(-1,1),frandom(-1,1))+ovel;
			}
			
			if(hd_debug)console.printf("Giving player ["..owner.playernumber().."] "..remainder.." Blues.");
			
			owner.A_GiveInventory("HDBlues", remainder);
			destroy();
		}
		else
		{

			if(hd_debug)console.printf("Hanging instance of HDBlueBlessingGiver destroyed.");
			destroy();
		}
	}
	
	override void loadoutconfigure(string input)
	{
			amount = getloadoutvar(input,"amount");
	}
}