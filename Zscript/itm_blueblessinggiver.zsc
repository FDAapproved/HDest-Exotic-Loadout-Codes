const RFID_BLUEBLESSING="bls";


// Blue Blessing.
class HDBlueBlessingGiver:HDPickup
{
	default
	{
		-hdpickup.fitsinbackpack;
		+HDPickup.NeverShowInPickupManager;
		hdpickup.refid RFID_BLUEBLESSING;
		tag "Healing Energy";
		inventory.icon "PLSEB0";
		// Easier to get more of these via insurgent this way. 
		hdpickup.bulk 3;
	}
	
	
	// Never shows in pickup manager, 
	// gives all directly to the player.
	override void postbeginplay()
	{	
	
		if(owner)
		{
			owner.A_StartSound("misc/p_pkup",CHAN_BODY);
			double oradius=owner.radius;
			double oheight=owner.height;
			vector3 ovel=owner.vel;
			
			int remainder = owner.countinv("HDBlueBlessingGiver");
			for(int i=0;i<10;i++){
				vector3 rpos=owner.pos+(
					frandom(-oradius,oradius),
					frandom(-oradius,oradius),
					frandom(0,oheight)
				);
				actor spk=actor.spawn("BluesSpark",rpos,ALLOW_REPLACE);
				spk.vel=(frandom(-1,1),frandom(-1,1),frandom(-1,1))+ovel;
			}

			if(hd_debug)console.printf("Giving player ["..owner.playernumber().."] "..remainder.." Blues.");
			owner.A_GiveInventory("HDBlues", remainder);
			owner.A_TakeInventory("HDBlueBlessingGiver", remainder);
		}
		else
		{
			if(level.time > 1)
			{
				if(hd_debug)console.printf("World instance of HDBlueBlessingGiver converted.");
				spawn("BluePotion", pos);
			}
			else
			{
				if(hd_debug)console.printf("Hanging instance of HDBlueBlessingGiver deleted.");
			}
			destroy();
		}
	}
}